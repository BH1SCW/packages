# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Martin Panter <vadmium+aur@gmail.com>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>

pkgname=mplayer2
pkgver=20120517
pkgrel=1
pkgdesc="An advanced general-purpose media player. A fork of the original MPlayer project"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
install=$pkgname.install
depends=('a52dec' 'aalib' 'cdparanoia' 'desktop-file-utils' 'enca' 'faad2' 'ffmpeg' 'fontconfig'
         'freetype2' 'ladspa' 'lame' 'libass' 'libcaca' 'libcdio' 'libdca' 'libdvdcss'
         'libdvdnav' 'libdvdread' 'libjpeg' 'libmad' 'libtheora' 'libvdpau'
         'libxinerama' 'libxss' 'libxv' 'libxxf86vm' 'mpg123' 'ncurses')
makedepends=('mesa' 'unzip' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer')
conflicts=('mplayer')
options=(!emptydirs)
source=(ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('814cc9545937e13796df1d4747c3c5d0')

build() {
    cd "${srcdir}"/$pkgname-$pkgver

    ./configure --prefix=/usr \
                --disable-runtime-cpudetection \
                --disable-lirc \
                --disable-lircc \
                --disable-bluray \
                --disable-speex \
                --disable-openal \
                --disable-libdv \
                --disable-musepack \
                --disable-mga \
                --disable-pulse \
                --disable-jack \
                --disable-ossaudio \
                --disable-gl \
                --disable-dga2 \
                --disable-dga1 \
                --disable-sdl \
                --disable-v4l2 \
                --disable-dvb \
                --language=all \
                --enable-translation \
                --confdir=/etc/mplayer
    make
}

package() {
    cd "${srcdir}"/$pkgname-$pkgver

    make DESTDIR="${pkgdir}" install
    install -Dm644 etc/{codecs.conf,input.conf,example.conf} "${pkgdir}"/etc/mplayer/
    install -dm755 "${pkgdir}"/usr/share/mplayer/

    install -dm755 "${pkgdir}"/usr/share/applications/
    install -m 644 etc/mplayer.desktop "${pkgdir}"/usr/share/applications/
}
