# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>

_gitname=libgit2
pkgname=libgit2-git
pkgver=4756.000e689
epoch=1
pkgrel=1
pkgdesc='A linkable library for Git'
arch=('i686' 'x86_64')
url="http://libgit2.github.com/"
license=('GPL2')
depends=(zlib openssl)
makedepends=(cmake python git)
provides=('libgit2')
conflicts=('libgit2')
source=(git://github.com/libgit2/libgit2.git)
md5sums=(SKIP)

pkgver() {
  cd $_gitname
  echo $(git rev-list --count development).$(git rev-parse --short development)
}

build() {
  cd $_gitname
  export LANG=en_US.UTF-8 # needed to build in the devtools container, sadly
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

check() {
  make -C $_gitname test
}

package() {
  make -C $_gitname DESTDIR="$pkgdir" install
}
