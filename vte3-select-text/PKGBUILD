# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=vte3-select-text
pkgver=0.32.2
pkgrel=4
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
depends=('gtk3' 'vte-common')
makedepends=('intltool' 'gobject-introspection' 'gtk3')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/vte/${pkgver%.*}/vte-$pkgver.tar.xz
        expose_select_text.patch)
sha256sums=('d69ec071f7a380d8b8b981b35a9a0b891f37c4b6b17d08407f66ac98d619486a'
            'd898a4500b810a23d8cd0fb941d6872707afa48c3621ef4523748b2504eb03b0')
provides=(vte3)
conflicts=(vte3)

build() {
  patch -p0 -i ../expose_select_text.patch
  cd "vte-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection
  make
}

package() {
  cd "vte-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/lib/vte/gnome-pty-helper
}
