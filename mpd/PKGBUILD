# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd
pkgver=0.16.8
pkgrel=2
pkgdesc='Flexible, powerful, server-side application for playing music'
url='http://mpd.wikia.com/wiki/Music_Player_Daemon_Wiki'
license=('GPL')
arch=('i686' 'x86_64')
depends=('ffmpeg' 'libmodplug' 'audiofile' 'libshout' 'libmad' 'curl' 'faad2'
         'sqlite' 'libmms' 'wavpack' 'libmpcdec' 'avahi' 'libid3tag')
source=("http://downloads.sourceforge.net/musicpd/${pkgname}-${pkgver}.tar.bz2"
        'mpd')
sha1sums=('977c80db8dc64e65c2bc523f69a9a7a71adca2b1'
          '3777bdb4fff4b7911be3b1242aabae9d2912ef18')

install=$pkgname.install

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-lastfm \
    --disable-ao \
    --disable-jack \
    --disable-pulse \
    --enable-documentation \
    --disable-libwrap \
    --disable-cue \
    --disable-sidplay \
    --disable-documentation \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed \
    -e '/^#playlist_directory/c playlist_directory "/var/lib/mpd/playlists"' \
    -e '/^#db_file/c db_file "/var/lib/mpd/mpd.db"' \
    -e '/^#pid_file/c pid_file "/run/mpd/mpd.pid"' \
    -e '/^#state_file/c state_file "/var/lib/mpd/mpdstate"' \
    -e '/^#user/c user "mpd"' \
    -i doc/mpdconf.example

  install -Dm755 ../mpd "${pkgdir}"/etc/rc.d/mpd
  install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd/playlists
  install -Dm644 doc/mpdconf.example "${pkgdir}"/usr/share/mpd/mpd.conf.example
}
