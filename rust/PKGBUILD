# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Boris Shomodjvarac <spam AT shomodj DOT com>
# Contributor: <s.astanin AT gmail DOT com>
# Contributor: Chris Bolton <chris AT pixelgen DOT co>
# Contributor: Thomas Dziedzic <gostrc AT gmail DOT com>
pkgname=rust
pkgver=0.5
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language from Mozilla.'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('libffi' 'perl' 'python2' 'curl')
source=("http://static.rust-lang.org/dist/rust-${pkgver}.tar.gz"
        "CFG_PYTHON.patch")
sha256sums=('d326d22707f0562d669c11efbc33ae812ddbf76ab78f07087fc5beb095a8928a'
            'f7b274da6882b2fdc6d003018a7c9885632518e53f49439124575e9a849faabe')

build() {
  cd rust-${pkgver}

  # https://github.com/mozilla/rust/commit/486217d7a2779d35c1177c7b1b7e96c1e5d30441
  patch -p1 -i ../CFG_PYTHON.patch

  ./configure --prefix=${pkgdir}/usr --disable-docs

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules

  make
}

package() {
  cd rust-${pkgver}

  make install

  install -d ${pkgdir}/usr/share/licenses/rust
  install -m644 LICENSE-APACHE ${pkgdir}/usr/share/licenses/rust
  install -m644 LICENSE-MIT ${pkgdir}/usr/share/licenses/rust
}
