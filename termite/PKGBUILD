# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=termite
pkgver=3
pkgrel=1
pkgdesc="A simple VTE-based terminal"
arch=('i686' 'x86_64')
url="https://github.com/thestinger/termite/"
license=('GPL')
depends=('vte3-select-text>=0.32.2-6')
makedepends=('git')
backup=(etc/xdg/termite.cfg)

__gitroot=https://github.com/thestinger/termite
__gitname=termite

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$__gitname" ]]; then
    cd "$__gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$__gitroot" "$__gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$__gitname-build"
  git clone "$srcdir/$__gitname" "$srcdir/$__gitname-build"
  cd "$srcdir/$__gitname-build"

  git checkout v$pkgver

  make
}

package() {
  cd "$srcdir/$__gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 termite.cfg "$pkgdir/etc/xdg/termite.cfg"
}
