# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris Baker <baker.chris.3@gmail.com>

pkgname=python2-nltk-git
pkgver=20120311
pkgrel=1
pkgdesc='Software for natural language processing'
arch=('any')
url='http://www.nltk.org/'
license=('APACHE')
depends=('python2-yaml')
optdepends=('nltk-data: test data'
            'python2-numpy: used for calculations'
            'python2-matplotlib: used for plotting')
provides=('python2-nltk')
makedepends=(git)
conflicts=('python2-nltk')

_gitroot="https://github.com/nltk/nltk.git"
_gitname="nltk"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname-build"

  python2 setup.py install --root=${pkgdir} --optimize=1
}
